buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.0.0'
    }
}

plugins {
    id 'java'
    id "com.github.spotbugs" version "4.2.4" apply false
    id 'org.cadixdev.licenser' version '0.5.1' apply false
}

allprojects {
    apply plugin: "com.github.spotbugs"

    group 'com.velocitypowered'
    version '1.1.2-SNAPSHOT'

    ext {
        // dependency versions
        textVersion = '3.0.4'
        adventureVersion = '4.1.1'
        adventurePlatformVersion = '4.0.0-SNAPSHOT'
        junitVersion = '5.7.0'
        slf4jVersion = '1.7.30'
        log4jVersion = '2.13.3'
        nettyVersion = '4.1.52.Final'
        guavaVersion = '25.1-jre'
        checkerFrameworkVersion = '3.6.1'
        configurateVersion = '3.7.1'

        getCurrentShortRevision = {
            new ByteArrayOutputStream().withStream { os ->
                exec {
                    executable = "git"
                    args = ["rev-parse", "HEAD"]
                    standardOutput = os
                }
                return os.toString().trim().substring(0, 8)
            }
        }
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    repositories {
        mavenLocal()
        mavenCentral()

        // for kyoripowered dependencies
        maven {
            url 'https://oss.sonatype.org/content/groups/public/'
        }

        // Brigadier
        maven {
            url "https://libraries.minecraft.net"
        }
    }

    test {
        reports {
            junitXml.enabled = true
        }
    }

    tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
        reports {
            html {
                enabled = true
                destination = file("$buildDir/reports/spotbugs/main/spotbugs.html")
                stylesheet = 'fancy-hist.xsl'
            }
        }
    }
}
